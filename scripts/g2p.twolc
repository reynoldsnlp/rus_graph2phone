Alphabet

      %  ! space
      %-

      Б  В  Г  Д  Ж З  Й:Й' К  Л  М  Н  П  Р  С  Т  Ф  Х  Ц Ч:Ч' Ш Щ:Щ'
      Б' В' Г' Д'   З' Й'   К' Л' М' Н' П' Р' С' Т' Ф' Х'   Ч'     Щ'
      Ъ:0 Ь:0
      А Е:Ь Ё:О́ И  О У Ы Э Ю  Я
      А́ Е́:Э́ Ё́   И́  О́ У́ Ы́ Э́ Ю́  Я́
      А̀ Ѐ   Ё̀   Ѝ  О̀ У̀ Ы̀ Э̀ Ю̀  Я̀
        Йэ  Йо  Йи         Йу Йа
        Йэ́  Йо́  Йи́         Йу́ Йа́
! TODO convert lowercase special characters to uppercase
      б  в  г  д  ж з  й:й' к  л  м  н  п  р  с  т  ф  х  ц ч:ч' ш щ:щ'
      б' в' г' д'   з' й'   к' л' м' н' п' р' с' т' ф' х'   ч'     щ'
      ъ:0 ь:0
      а:ъ   е:ь  ё:о́  и  о:ъ у ы э   ю:у  я:ь ʌ
      а́   â е́:э́  ё́    и́  о́   у́ ы́ э́ э̂ ю́:у́  я́:а́     ! ё́ is included to broaden coverage
      а̀     ѐ    ё̀    ѝ  о̀   у̀ ы̀ э̀   ю̀    я̀
            йэ:ь йо   йи             йу   йа:ь
            йэ́   йо́   йи́             йу́   йа́ ;

Sets

Letter = А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
         А́         Е́ Ё́     И́           О́         У́               Ы́   Э́ Ю́ Я́
         А̀         Ѐ Ё̀     Ѝ           О̀         У̀               Ы̀   Э̀ Ю̀ Я̀
         а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я
         а́         е́ ё́     и́           о́         у́               ы́   э́ ю́ я́
         а̀         ѐ ё̀     ѝ           о̀         у̀               ы̀   э̀ ю̀ я̀
         %- %  ;

! Consonants
C = Б  В  Г  Д  Ж З  Й  К  Л  М  Н  П  Р  С  Т  Ф  Х  Ц Ч  Ш Щ
    Б' В' Г' Д'   З' Й' К' Л' М' Н' П' Р' С' Т' Ф' Х'   Ч'   Щ'
    б  в  г  д  ж з  й  к  л  м  н  п  р  с  т  ф  х  ц ч  ш щ
    б' в' г' д'   з' й' к' л' м' н' п' р' с' т' ф' х'   ч'   щ' ;

! Consonants, excluding { Д д Т т }
CNoDT = Б В Г Ж З Й К Л М Н П Р С Ф Х Ц Ч Ш Щ
        б в г ж з й к л м н п р с ф х ц ч ш щ ;

! Consonants
CNoHard = Б В Г Д З Й К Л М Н П Р С Т Ф Х Ч Щ
          б в г д з й к л м н п р с т ф х ч щ ;

! Consonants, excluding { Ж ж Й й Ч ч Ш ш Щ щ Ц ц }
CNoSoftHard = Б В Г Д З К Л М Н П Р С Т Ф Х
              б в г д з к л м н п р с т ф х ;

! Unvoiced consonants
UnvoicedC = К П С Т Ф Х Ц Ч Ш Щ
            к п с т ф х ц ч ш щ ;

! Unvoiced and paired consonants
UnvoicedPairedC = К К' П П' С С' Т Т' Ф Ф' Ш
                  к к' п п' с с' т т' ф ф' ш ;

! Voiced and paired consonants
VoicedPairedC = Б Б' В В' Г Г' Д Д' Ж З З'
                б б' в в' г г' д д' ж з з' ;

! Soft consonants
SoftC = Й Ч Щ
        й ч щ ;

! Hard consonants
HardC = Ж Ц Ш
        ж ц ш ;

! Hushers
HUSH = Ж Ш Щ Ч
       ж ш щ ч ;

! Labials for Softness Assimilation
LabS = Б' б' В' в' М' м' П' п' Ф' ф' ;

! Dentals for Softness Assimilation
DenS = Д' д' З' з' Л' л' Н' н' Р' р' С' с' Т' т' Ц ц ;

! Palatals for Softness Assimilation
PalS = Й' й' Ч' ч' ;

! Velars for Softness Assimilation
VelS = Г' г' К' к' Х' х' ;

! Paired consonants (consonants that have both a hard and soft variant, excluding Velars)
PC = Б В Д З Л М Н П Р С Т Ф
     б в д з л м н п р с т ф ;

! Paired and inherently soft consonants (without soft marking)
PCandSoftH = Б  В  Г  Д  З  Й  К  Л  М  Н  П  Р  С  Т  Ф  Х  Ч  Щ
              б  в  г  д  з  й  к  л  м  н  п  р  с  т  ф  х  ч  щ ;

! Paired and inherently soft consonants (with soft marking)
PCandSoftS = Б' В' Г' Д' З' Й' К' Л' М' Н' П' Р' С' Т' Ф' Х' Ч' Щ'
              б' в' г' д' з' й' к' л' м' н' п' р' с' т' ф' х' ч' щ' ;

! Paired consonants, excluding { Н н }
PCNoN = Б В Д З Л М П Р С Т Ф
        б в д з л м п р с т ф ;

! Vowels
V = А Е Ё И О У Ы Э Ю Я
    А́ Е́ Ё́ И́ О́ У́ Ы́ Э́ Ю́ Я́
    А̀ Ѐ Ё̀ Ѝ О̀ У̀ Ы̀ Э̀ Ю̀ Я̀
    а е ё и о у ы э ю я
    а́ е́ ё́ и́ о́ у́ ы́ э́ ю́ я́
    а̀ ѐ ё̀ ѝ о̀ у̀ ы̀ э̀ ю̀ я̀ ;

! Stressed vowels
StressedV = А́ Е́ Ё́ Ё И́ О́ У́ Ы́ Э́ Ю́ Я́
            а́ е́ ё́ ё и́ о́ у́ ы́ э́ ю́ я́ ;

! Unstressed vowels
UnstressedV = А Е И О У Ы Э Ю Я
              а е и о у ы э ю я ;

! Vowels that indicate softness
SoftV = Е Ё И Ю Я
        Е́ Ё́ И́ Ю́ Я́
        е ё и ю я
        е́ ё́ и́ ю́ я́ ;

! Vowels that indicate hardness
HardV = А О У Ы Э
        а о у ы э ;


Definitions

! Beginning = [ а́:0 | о́:0 | а:0 | о:0 | я́:0 | ё:0 | е́:0 | я:0 | е:0 | ь:0 | й:0 ] ;

Rules

!! ============================================================================
"Soften consonants before 'soft' vowels and ь (excluding { Ж ж Ц ц Ш ш Ч ч Щ щ Й й })"
Cx:Cy <=> _ [ SoftV: | ь: ] ;
!!€ ся́ду
!!€ с'а́ду
!!€ твёрдый
!!€ тв'о́рдый

where Cx in ( Б  В  Г  Д  З  К  Л  М  Н  П  Р  С  Т  Ф  Х
              б  в  г  д  з  к  л  м  н  п  р  с  т  ф  х  )
      Cy in ( Б' В' Г' Д' З' К' Л' М' Н' П' Р' С' Т' Ф' Х'
              б' в' г' д' з' к' л' м' н' п' р' с' т' ф' х' )
    matched ;

!! ============================================================================
"ю>йу between vowels, word-initially, and after hard-/soft-signs"
Vx:Vy <=> [ V: | Ъ: | Ъ: | .#. ] _ ;

where Vx in ( Ю́  Ю  ю́  ю )
      Vy in ( Йу́ Йу йу́ йу )
    matched ;

!! ============================================================================
"Yeri rule: after hard consonants и is pronounced as ы"
Vx:Vy <=> HardC _ ;
where Vx in ( И́ И и́ и )
      Vy in ( Ы́ Ы ы́ ы )
  matched ;

!! ============================================================================
"Akanje: Unstressed vowel reduction for { о а } in pre-tonic location and word-initially"
!TODO: Doesn't work for крою́
Vx:Vy <=> _ [:C | ь:]* StressedV: ;
          .#. _ ;
where Vx in ( о а )
      Vy in ( ʌ ʌ )
  matched ;

!! ============================================================================
"Ikanje: Unstressed vowel reduction for { я е } in pre-tonic location"
Vx:Vy <=> [: - [ HardC: | V: ]] _ [:C | ь:]* StressedV: ;
where Vx in ( Я я Е е )
      Vy in ( И и И и )
 matched ;

!! ============================================================================
"Ikanje: Unstressed vowel reduction for { я е } in non-pre-tonic location and word-initially"
Vx:Vy <=> [: - HardC: ] _ [ [ C: | ь: ]* UnstressedV: [ C: | ь:]* ]+ StressedV: ;
          .#. _ [ [ C: | ь: ]* UnstressedV: [ C: | ь:]* ]+ StressedV: ;
where Vx in ( Я я Е е )
     Vy in ( Йь йь Йь йь )
matched ;

!! ============================================================================
"Ikanje: Unstressed vowel reduction for { я е } in pre-tonic location"
Vx:Vy <=> [ V: | .#. ] _ [:C | ь:]* StressedV: ;
where Vx in ( Я я Е е )
     Vy in ( Йи йи Йи йи )
matched ;

!! ============================================================================
"Stressed vowel realization of Й for { я е }"
Vx:Vy <=> [ V: | Ъ: | Ъ: | .#. ] _ ;
where Vx in ( Я́ я́ Е́ е́ Ё ё Ё́ ё́ )
     Vy in ( Йа́ йа́ Йэ́ йэ́ Йо́ йо́ Йо́ йо́)
matched ;

!!! ============================================================================
!"Ikanje: Vowel reduction for { я е ё } in at beginning and after vowel"
! TODO: this won't work because the vowels are manipulated later
! It needs to take 0 -> Й or й
!Vx:Vy <=> [ V: | Ъ: | Ъ: | .#. ] _ [ [:C | ь:]* StressedV: ] ] ;
!where Vx in ( Я  Я́  я  я́  Е  Е́  е  е́  Ё  ё  )
!      Vy in ( Йа Йа́ йа йа́ Йэ Йэ́ йэ йэ́ Йо́ йо́ )
!matched ;
!
!
!!! ============================================================================
!"Vowel reduction for { и } in at beginning and after { ъ ь }"
! TODO: this won't work because the vowels are manipulated later
! It needs to take 0 -> Й or й
!Vx:Vy <=> [ Ъ: | Ъ: | .#. ] _ ;
!where Vx in ( И  и  )
!      Vy in ( Йи йи )
!matched ;
!
!! ============================================================================
!TODO: In endings like ят it could be a noun and thus wouldn't have the same pronunciation as the verb form
! алкоголя́т (although this is stressed, are there any other examples?)
! TODO: identify where this is misfiring (e.g. wrong pos)
!"Ikanje: Unstressed vowel reduction for { я } in word-ending"
!Vx:Vy <=> _ [ т: | т: с: я: | с: ь: | х: | м: | м: и: ]? .#. ;
!
!where Vx in ( Я я )
!      Vy in ( Ъ ъ )
! matched ;
!
!! ============================================================================
"Ykanje: unstressed vowel reduction for { е } after { ж ш ц } in pre-tonic location"
Vx:Vy <=> .#. [: - StressedV: ]* HardC: _ [ C: | ь: ]* StressedV: ;
where Vx in ( Е е )
      Vy in ( Ы ы )
  matched ;

!! ============================================================================
"Ykanje: unstressed е after { ж ш ц } in non-pre-tonic location"
Vx:Vy <=> HardC: _ [ [ C: | ь: ]* UnstressedV: [ C: | ь:]* ]+ StressedV: ;
          StressedV: :* HardC: _ ;
where Vx in ( Е е )
      Vy in ( Ъ ъ )
  matched ;

!! ============================================================================
"Tense e"
Vx:Vy <=> _ :PCandSoftS ;
where Vx in ( É é )
      Vy in ( Ê ê )
  matched ;

!! ============================================================================
"Tense я"
Vx:Vy <=> :PCandSoftS _ :PCandSoftS ;
where Vx in ( Я́ я́ А́ а́ )
      Vy in ( А̂ а̂ А̂ а̂ )
  matched ;

!! ============================================================================
"Voice Assimilation: Final devoicing"
Cx:Cy <=>  _ .#. ;
where Cx in ( Б Б' б б' В В' в в' Г Г' г г' Д Д' д д' Ж ж З З' з з' )
      Cy in ( П П' п п' Ф Ф' ф ф' К К' к к' Т Т' т т' Ш ш С С' с с' )
  matched ;

!! ============================================================================
"Voice Assimilation: Consonant clusters with unvoiced consonant at end of the cluster"
!TODO: В doesn't force softness assimilation
Cx:Cy <=>  _ :Cy ;
where Cx in ( Б Б' б б' В В' в в' Г Г' г г' Д Д' д д' Ж ж З З' з з' )
      Cy in ( П П' п п' Ф Ф' ф ф' К К' к к' Т Т' т т' Ш ш С С' с с' )
  matched ;

!! ============================================================================
"Voice Assimilation: Consonant clusters with voiced consonant at end of the cluster"
Cx:Cy <=>  _ :Cy ;
where Cx in ( П П' п п' Ф Ф' ф ф' К К' к к' Т Т' т т' Ш ш С С' с с' )
      Cy in ( Б Б' б б' В В' в в' Г Г' г г' Д Д' д д' Ж ж З З' з з' )
  matched ;

!! ============================================================================
"Softness Assimilation: Dentals"
Cx:Cy <=> _ :DenS ;
where Cx in ( Д  д  З  з  Л  л  Н  н  Р  р  С  с  Т  т  )
      Cy in ( Д' д' З' з' Л' л' Н' н' Р' р' С' с' Т' т' )
    matched ;

!! ============================================================================
"Softness Assimilation: Labials"
Cx:Cy <=> _ :LabS ;
where Cx in ( Б  б  В  в  М  м  П  п  Ф  ф  )
      Cy in ( Б' б' В' в' М' м' П' п' Ф' ф' )
    matched ;

!! ============================================================================
"Softness Assimilation: Velar"
Cx:Cy <=> _ :VelS ;
where Cx in ( Г  г  К  к  Х  х  )
      Cy in ( Г' г' К' к' Х' х' )
    matched ;

!! ============================================================================
!"Conversion of { ого его } in endings to { ово 'эво }"
!TODO: Do we just list out several words that change this way?

!! ============================================================================
!"Conversion of { тс } in endings { ятся } to { ц }"
!TODO:
